package analizadores.estructuraGramatica;
 
import analizadores.objetos.ErrorAnalisis;
import analizadores.objetos.componentes.NodoER.*;
import analizadores.objetos.TablaDeSimbolos;
import analizadores.objetos.Variable;
import analizadores.objetos.componentes.lexer.GeneradorAutomata;
import analizadores.objetos.componentes.lexer.PR;
import analizadores.objetos.componentes.Dato;
import analizadores.objetos.componentes.Informacion;
import analizadores.objetos.componentes.parser.GeneradorParser;
import analizadores.objetos.componentes.parser.Produccion;
import analizadores.objetos.componentes.parser.Simbolo;
import java.util.ArrayList;
import java.util.List;
import java_cup.runtime.Symbol;

action code {::}

parser code {:
    
        private TablaDeSimbolos expresionesRegulares, simbolosGramatica;
        private List<ErrorAnalisis> listErrores;
        private List<String> cadenasTemporales;
        private GeneradorAutomata generadorAutomata;
        private GeneradorParser generadorParser;
        private Informacion informacionLenguaje;

        public ParserGramatica(LexerGramatica lex) { 
            super(lex);   
            listErrores = lex.getErrores();
            generadorAutomata = new GeneradorAutomata();
            generadorParser = new GeneradorParser();
            expresionesRegulares = new TablaDeSimbolos();
            simbolosGramatica = new TablaDeSimbolos();
            cadenasTemporales = new ArrayList<String>();
            informacionLenguaje = new Informacion();
	}
        
        public void setAccion(String tipo, String mensaje, String accion){
            System.out.println(tipo+"         "+mensaje);
        }

        public void unrecovered_syntax_error(Symbol cur_token){
            if(cur_token.toString().equals("#0")){
                listErrores.add(new ErrorAnalisis("Sintactico","Fin del archivo", "No se pudo recuperar y seguir analizando, no se encontraron mas tokens.",cur_token.left,cur_token.right));
            }else{
                listErrores.add(new ErrorAnalisis("Sintactico",(String)cur_token.value, "No se pudo recuperar y seguir analizando.",cur_token.left,cur_token.right));
            }
        }

        public void syntax_error(Symbol s){
            String message = "";
            StringBuilder m = new StringBuilder("Error: ");
            String cadena = (String) s.value;
            int linea = s.left;
            int columna = s.right;
            switch(s.toString()){
                case "#1":{ message = " No se esperaba la cadena Â¿error? "; break;}
                case "#2": case "#3": case "#4": case "#5": case "#6": case "#32": case "#33": case "#34":  case "#35": case "#36": { 
                    message = " No se esperaba la palabra reservada '"+cadena+"'"; break;  
                }
                case "#7": case "#8": case "#9":{ message = " No se esperaba una asignacion del tipo '"+cadena+"'"; break; }
                case "#10": { message = " No se esperaba un separador de seccion '%%'"; break; }
                case "#11": case "#12": case "#13": case "#14": { message = " No se esperaba el operador de expresiones regulares '"+cadena+"'"; break;}
                case "#15": case "#16": case "#18": case "#19": { message = " No se esperaba un caracter de agrupacion '"+cadena+"'"; break; }
                case "#17":{ message = " No se esperaba un caracter explicito '"+cadena+"'"; break;}
                case "#20":{ message = " No se esperaba un fin de linea ';'"; break; }
                case "#21":{ message = " No se esperaba un salto de linea '\\n'"; break; }
                case "#22":{ message = " No se esperaba una tabulacion '\\t'"; break; }
                case "#23":{ message = " No se esperaba un retorno '\\b'"; break; }
                case "#24":{ message = " No se esperaba el simbolo '&'"; break; }
                case "#25":{ message = " No se esperaba una cadena <"+cadena+">"; break; }
                case "#26":{ message = " No se esperaba codigo java"; break;}
                case "#27":{ message = " No se esperaba un entero '"+cadena+"'"; break;}
                case "#28":{ message = " No se esperaba una version '"+cadena+"'"; break;}
                case "#29":{ message = " No se esperaba un identificador '"+cadena+"'"; break;}
                case "#30":{ message = " No se esperaba un rango de numeros"; break; }
                case "#31":{ message = " No se esperaba un rango de letras"; break; }
                case "#37":{ message = " No se esperaba una coma"; break; }
                case "#38":{ message = " No se esperaba un identificador de simbolo terminal '"+cadena+"'"; break; }
                case "#39":{ message = " No se esperaba un identificador de simbolo no terminal '"+cadena+"'"; break; }
            }
            listErrores.add(new ErrorAnalisis("Sintactico",cadena,message,linea,columna));
        }

        public List<ErrorAnalisis> getErrores(){return listErrores;}
        public void setErrores(List<ErrorAnalisis> e){listErrores = e;}
        public GeneradorAutomata getGeneradorAutomata(){ return generadorAutomata; }
        public GeneradorParser getGeneradorParser() { return generadorParser; }
        public TablaDeSimbolos getTablaDeER(){ return expresionesRegulares; }
        public TablaDeSimbolos getTablaDeSimbolosGramaticales(){ return simbolosGramatica;}
        public Informacion getInformacion(){ return informacionLenguaje; }

:}

/* terminales y no terminales */

terminal String                                    PR_NOMBRE, PR_AUTOR, PR_VERSION, PR_LANZAMIENTO, PR_EXTENSION, ASIGNACION_INF, ASIGNACION_GRAMA;
terminal String                                    ASIGNACION_ER, SEPARADOR, UNA_O_MAS_VECES, CERO_O_MAS_VECES, PUEDE_O_NO_PUEDE, O, COR_A, COR_C, CARACTER_EXPLICITO;
terminal String                                    PAR_A, PAR_C, FIN_DE_LINEA, SALTO_DE_LINEA, TABULACION, RETORNO, IGNORAR, CADENA, JAVA_CODE, ENTERO;
terminal String                                    VERSION, ID, RANGO_NUMEROS, RANGO_LETRAS_MIN, PR_TERMINAL, PR_NO, PR_ENTERO, PR_REAL, PR_CADENA, COMA;
terminal String                                    ID_T, ID_NT;

non terminal                                s, seccionER;
non terminal                                seccionSimbolos, declaracionesSimbolos, declaracionSimbolo, seccionGramatica;
non terminal                                produccion, tipoSimbolo, codigoJava;
non terminal String                         idCompuesto;
non terminal                                seccionInformacion, dato; 
non terminal List<Simbolo>                  listadoSimbolos, listadoIdsMinus, listadoIdsMayus;
non terminal Simbolo                        simboloProduccion;
non terminal Nodo                           expresionesRegulares, declaracionExpresionRegular, expresionRegular;

/* precedencia */

precedence left     SALTO_DE_LINEA, TABULACION, RETORNO, CADENA;
precedence left     O, RANGO_LETRAS_MIN, RANGO_NUMEROS, ENTERO, VERSION, ID_T;
precedence left     CARACTER_EXPLICITO;
precedence left     UNA_O_MAS_VECES, CERO_O_MAS_VECES, PUEDE_O_NO_PUEDE;
precedence left     PAR_A, COR_A, PAR_C, COR_C;

/* GRAMATICA */

start with s;

s::=                                        seccionInformacion SEPARADOR SEPARADOR seccionER {:
                                                if(informacionLenguaje.getNombre() == null){
                                                    listErrores.add(new ErrorAnalisis("Semantico","<sin cadena>","En la seccion informacion, debe al menos especiicarse el nombre del lenguaje",0,0));
                                                }
                                            :}
                                            |error SEPARADOR seccionER
                                            ;

seccionInformacion::=                       seccionInformacion dato
                                            |dato
                                            ;

dato::=                                     PR_NOMBRE:tipo ASIGNACION_INF idCompuesto:valor FIN_DE_LINEA {:
                                                Dato dato = new Dato();
                                                if(valor != null) dato = new Dato("Nombre",valor);
                                                informacionLenguaje.agregarDato(dato, tipoleft, tiporight, listErrores);
                                            :}
                                            |PR_LANZAMIENTO:tipo ASIGNACION_INF ENTERO:valor FIN_DE_LINEA {:
                                                Dato dato = new Dato();
                                                if(valor != null) dato = new Dato("Lanzamiento",valor);
                                                informacionLenguaje.agregarDato(dato, tipoleft, tiporight, listErrores);
                                            :}
                                            |PR_VERSION:tipo ASIGNACION_INF VERSION:valor FIN_DE_LINEA {:
                                                Dato dato = new Dato();
                                                if(valor != null) dato = new Dato("Version",valor);
                                                informacionLenguaje.agregarDato(dato, tipoleft, tiporight, listErrores);
                                            :}
                                            |PR_AUTOR:tipo ASIGNACION_INF idCompuesto:valor FIN_DE_LINEA {:
                                                Dato dato = new Dato();
                                                if(valor != null) dato = new Dato("Autor",valor);
                                                informacionLenguaje.agregarDato(dato, tipoleft, tiporight, listErrores);
                                            :}
                                            |PR_EXTENSION:tipo ASIGNACION_INF ID:valor FIN_DE_LINEA {:
                                                Dato dato = new Dato();
                                                if(valor != null) dato = new Dato("Extension",valor);
                                                informacionLenguaje.agregarDato(dato, tipoleft, tiporight, listErrores);
                                            :}
                                            |error FIN_DE_LINEA
                                            ;

seccionER::=                                expresionesRegulares:nodo SEPARADOR seccionSimbolos {:
                                                generadorAutomata.setExpresionRegular(nodo);
                                            :}
                                            |error SEPARADOR seccionSimbolos
                                            ;

expresionesRegulares::=                     expresionesRegulares:nodos declaracionExpresionRegular:nodo {:
                                                Nodo nodoUnion = new NodoHoja();
                                                if(nodos != null && nodo != null){
                                                    nodoUnion = new NodoDis(nodos, nodo);
                                                }
                                                RESULT = nodoUnion;
                                            :}
                                            |declaracionExpresionRegular:nodo{:
                                                Nodo nodoSalida = new NodoHoja();
                                                if(nodo != null) nodoSalida = nodo;
                                                RESULT = nodoSalida;
                                            :}
                                            ;

declaracionExpresionRegular::=              ID_T:identificador ASIGNACION_ER expresionRegular:expresion FIN_DE_LINEA {:
                                                Nodo nodo = new NodoHoja();
                                                if(expresion != null) nodo = expresion;
                                                if(!expresionesRegulares.addVariable(new Variable(identificador, nodo))){
                                                    listErrores.add(new ErrorAnalisis("Semantico",identificador,"Ya existe una expresion regular con el identificador ingresado.",identificadorleft, identificadorright));
                                                    nodo = new NodoHoja();
                                                }else{
                                                    generadorAutomata.asignarTipoToken(identificador, nodo);
                                                    for(String cadena : cadenasTemporales){
                                                        generadorAutomata.getAutomata().getPalabrasReservadas().add(new PR(identificador, cadena));
                                                    }
                                                    cadenasTemporales.clear();
                                                }
                                                RESULT = nodo;
                                            :}
                                            |IGNORAR:identificador ASIGNACION_ER expresionRegular:expresion FIN_DE_LINEA {:
                                                Nodo nodo = new NodoHoja();
                                                if(expresion != null) nodo = expresion;
                                                if(!expresionesRegulares.addVariable(new Variable(identificador, nodo))){
                                                    listErrores.add(new ErrorAnalisis("Semantico",identificador,"Ya existe una expresion regular con el identificador ingresado.",identificadorleft, identificadorright));
                                                    nodo = new NodoHoja();
                                                }else{
                                                    generadorAutomata.asignarTipoToken(identificador, nodo);
                                                }
                                                RESULT = nodo;
                                            :}
                                            |error FIN_DE_LINEA
                                            ;

expresionRegular::=                         expresionRegular:expresion PUEDE_O_NO_PUEDE {:
                                                Nodo nodo = (expresion != null)? new NodoQuiza(expresion) : new NodoQuiza();
                                                RESULT = nodo;
                                            :}
                                            |expresionRegular:expresion CERO_O_MAS_VECES {:
                                                Nodo nodo = (expresion != null)? new NodoAst(expresion) : new NodoAst();
                                                RESULT = nodo;
                                            :}
                                            |expresionRegular:expresion UNA_O_MAS_VECES {:
                                                Nodo nodo = (expresion != null)? new NodoMas(expresion) : new NodoMas();
                                                RESULT = nodo;
                                            :}
                                            |expresionRegular:expresionIzquierda O expresionRegular:expresionDerecha {:
                                                Nodo nodo = new NodoDis();
                                                if(expresionIzquierda != null && expresionDerecha != null){
                                                    nodo = new NodoDis(expresionIzquierda, expresionDerecha);
                                                }
                                                RESULT = nodo;
                                            :}
                                            |expresionRegular:expresionIzquierda expresionRegular:expresionDerecha {:
                                                Nodo nodo = new NodoConcat();
                                                if(expresionIzquierda != null && expresionDerecha != null){
                                                    nodo = new NodoConcat(expresionIzquierda, expresionDerecha);
                                                }
                                                RESULT = nodo;
                                            :}
                                            |RANGO_LETRAS_MIN:valor {:
                                                Nodo nodo = generadorAutomata.agregarRango(valor, 0);
                                                RESULT = nodo;
                                            :}
                                            |RANGO_NUMEROS:valor {:
                                                Nodo nodo = generadorAutomata.agregarRango(valor, 1);
                                                RESULT = nodo;
                                            :}
                                            |SALTO_DE_LINEA {:
                                                Nodo nodo = new NodoHoja('\n');
                                                RESULT = nodo;
                                            :}
                                            |TABULACION {:
                                                Nodo nodo = new NodoHoja('\t');
                                                RESULT = nodo;
                                            :}
                                            |RETORNO {:
                                                Nodo nodo = new NodoHoja('\b');
                                                RESULT = nodo;
                                            :}
                                            |COR_A expresionRegular:expresion COR_C {:
                                                Nodo nodo = new NodoHoja();
                                                if(expresion != null) nodo = expresion;
                                                RESULT = nodo;
                                            :}
                                            |PAR_A expresionRegular:expresion PAR_C {:
                                                Nodo nodo = new NodoHoja();
                                                if(expresion != null) nodo = expresion;
                                                RESULT = expresion;
                                            :}
                                            |CARACTER_EXPLICITO:caracter {:
                                                Nodo nodo = new NodoHoja();
                                                if(caracter != null) nodo = new NodoHoja(caracter.charAt(0));
                                                RESULT = nodo;
                                            :}
                                            |CADENA:cadena {:
                                                Nodo nodo = new NodoConcat();
                                                if(cadena != null) nodo = generadorAutomata.agregarCadena(cadena.substring(1, cadena.length()-1));
                                                cadenasTemporales.add(cadena.substring(1,cadena.length()-1));
                                                RESULT = nodo;
                                            :}
                                            |ENTERO:valor {:
                                                Nodo nodo = new NodoHoja();
                                                if(valor != null) nodo = generadorAutomata.agregarCadena(valor);
                                                cadenasTemporales.add(valor);
                                                RESULT = nodo;
                                            :}
                                            |VERSION:valor {:
                                                Nodo nodo = new NodoHoja();
                                                if(valor != null) nodo = generadorAutomata.agregarCadena(valor);
                                                cadenasTemporales.add(valor);
                                                RESULT = nodo;
                                            :}
                                            |ID_T:valor {:
                                                Nodo nodo = new NodoHoja();
                                                if(!expresionesRegulares.verificarDisponibilidad(valor)){
                                                    Nodo nuevo = (Nodo) expresionesRegulares.getVariable(valor).getValor();
                                                    if(nuevo instanceof NodoConcat){
                                                        nodo = new NodoConcat((NodoConcat) nuevo);
                                                    }else if(nuevo instanceof NodoDis){
                                                        nodo = new NodoDis((NodoDis) nuevo);
                                                    }else if(nuevo instanceof NodoMas){
                                                        nodo = new NodoMas((NodoMas) nuevo);
                                                    }else if(nuevo instanceof NodoQuiza){
                                                        nodo = new NodoQuiza((NodoQuiza)nuevo);
                                                    }else if(nuevo instanceof NodoHoja){
                                                        nodo = new NodoHoja((NodoHoja) nuevo);
                                                    }
                                                }else{
                                                    listErrores.add(new ErrorAnalisis("Semantico",valor,"No existe una expresion regular declarada anteriormente con el identificador ingresado.",valorleft,valorright));
                                                    nodo = new NodoHoja();
                                                }
                                                RESULT = nodo;
                                            :}
                                            ;


seccionSimbolos::=                          declaracionesSimbolos SEPARADOR seccionGramatica 
                                            |error SEPARADOR seccionGramatica 
                                            ;

declaracionesSimbolos::=                    declaracionesSimbolos declaracionSimbolo
                                            |declaracionSimbolo
                                            ;

declaracionSimbolo::=                       PR_TERMINAL tipoSimbolo listadoIdsMinus:listadoIds FIN_DE_LINEA {:
                                                if(listadoIds == null) listadoIds = new ArrayList();
                                                for(Simbolo id : listadoIds){
                                                    id.setTerminal(true);
                                                    simbolosGramatica.addVariable(new Variable(id.getSimbolo(), id));
                                                }
                                            :}
                                            |PR_NO PR_TERMINAL tipoSimbolo listadoIdsMayus:listadoIds FIN_DE_LINEA{:
                                                if(listadoIds == null) listadoIds = new ArrayList();
                                                for(Simbolo id : listadoIds){
                                                    id.setTerminal(false);
                                                    simbolosGramatica.addVariable(new Variable(id.getSimbolo(), id));
                                                }
                                            :}
                                            |error FIN_DE_LINEA
                                            ;

seccionGramatica::=                         seccionGramatica produccion
                                            |produccion
                                            ;

produccion::=                               ID:parteIzquierda ASIGNACION_GRAMA listadoSimbolos:parteDerecha codigoJava FIN_DE_LINEA {:
                                                if(parteIzquierda != null && parteDerecha != null){
                                                    Produccion produccion = new Produccion(new Simbolo(parteIzquierda,parteIzquierdaleft,parteIzquierdaright), (ArrayList<Simbolo>)parteDerecha);
                                                    generadorParser.getProducciones().add(produccion);
                                                }
                                            :}
                                            |ID:parteIzquierda FIN_DE_LINEA {:
                                                if(parteIzquierda != null){
                                                    Produccion produccion = new Produccion(new Simbolo(parteIzquierda,parteIzquierdaleft,parteIzquierdaright), new ArrayList<Simbolo>());
                                                    generadorParser.getProducciones().add(produccion);
                                                } 
                                            :}
                                            |error FIN_DE_LINEA
                                            ;

listadoSimbolos::=                          listadoSimbolos:listadoSimbolos simboloProduccion:simbolo {:
                                                if(listadoSimbolos == null) listadoSimbolos = new ArrayList();
                                                if(simbolo != null) listadoSimbolos.add(simbolo);
                                                RESULT = listadoSimbolos;
                                            :}
                                            |simboloProduccion:simbolo {:
                                                List<Simbolo> simbolos = new ArrayList();
                                                if(simbolo != null) simbolos.add(simbolo);
                                                RESULT = simbolos;
                                            :}
                                            ;

simboloProduccion::=                        ID:id ASIGNACION_INF ID {:
                                                Simbolo simbolo = null;
                                                if(id != null) simbolo = new Simbolo(id, idleft, idright);
                                                RESULT = simbolo;
                                            :}
                                            |ID:id {:
                                                Simbolo simbolo = null;
                                                if(id != null) simbolo = new Simbolo(id, idleft, idright);
                                                RESULT = simbolo;
                                            :}
                                            ;

idCompuesto::=                              idCompuesto:cadena ID:valor {:
                                                if(cadena == null) cadena = "";
                                                if(valor != null) cadena+= " "+valor;
                                                RESULT = cadena;
                                            :}
                                            |ID:valor {:
                                                String cadena = "";
                                                if(valor != null) cadena = valor;
                                                RESULT = cadena;
                                            :}
                                            ;

tipoSimbolo::=                              PR_ENTERO
                                            |PR_REAL
                                            |PR_CADENA
                                            |
                                            ;

listadoIdsMinus::=                          listadoIdsMinus:listado COMA ID_T:id {:
                                                if(listado == null) listado = new ArrayList();
                                                if(simbolosGramatica.getVariable(id) != null){
                                                    listErrores.add(new ErrorAnalisis("Semantico",id,"Ya existe un simbolo declarado con el identificador ingresado.",idleft,idright));
                                                }else{
                                                    int precedencia = 0;
                                                    if(listado.size() >= 1) precedencia = listado.get(listado.size()-1).getPrecedencia()+1;
                                                    if(id != null) listado.add(new Simbolo(id, idleft, idright, precedencia));
                                                }
                                                RESULT = listado;
                                            :}
                                            |ID_T:id {:
                                                List<Simbolo> listado = new ArrayList();
                                                if(simbolosGramatica.getVariable(id) != null){
                                                    listErrores.add(new ErrorAnalisis("Semantico",id,"Ya existe un simbolo declarado con el identificador ingresado.",idleft,idright));
                                                }else{
                                                    int precedencia = 0;
                                                    for(Variable variable : simbolosGramatica.getVariables()){
                                                        Simbolo simbolo = (Simbolo) variable.getValor();
                                                        if(simbolo.isTerminal()) precedencia = simbolo.getPrecedencia()+1;
                                                    }
                                                    if(id != null) listado.add(new Simbolo(id, idleft, idright, precedencia));
                                                }
                                                RESULT = listado;
                                            :}
                                            ;

listadoIdsMayus::=                          listadoIdsMayus:listado COMA ID_NT:id {:
                                                if(listado == null) listado = new ArrayList();
                                                if(simbolosGramatica.getVariable(id) != null){
                                                    listErrores.add(new ErrorAnalisis("Semantico",id,"Ya existe un simbolo declarado con el identificador ingresado.",idleft,idright));
                                                }else{
                                                    if(id != null) listado.add(new Simbolo(id, idleft, idright));
                                                }
                                                RESULT = listado;
                                            :}
                                            |ID_NT:id {:
                                                List<Simbolo> listado = new ArrayList();
                                                if(simbolosGramatica.getVariable(id) != null){
                                                    listErrores.add(new ErrorAnalisis("Semantico",id,"Ya existe un simbolo declarado con el identificador ingresado.",idleft,idright));
                                                }else{
                                                    if(id != null) listado.add(new Simbolo(id, idleft, idright));
                                                }
                                                RESULT = listado;
                                            :}
                                            ;

codigoJava::=                               JAVA_CODE
                                            |
                                            ;