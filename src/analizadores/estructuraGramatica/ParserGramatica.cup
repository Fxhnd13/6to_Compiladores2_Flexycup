package analizadores.estructuraGramatica;

import java.util.List;
import java_cup.runtime.Symbol;
import java_cup.runtime.XMLElement;

action code {::}

parser code {:
    
        private List<String> listErrores;

        public ParserGramatica(LexerGramatica lex) { 
            super(lex);   
            listErrores = lex.getErrores();
	}
        
        public void setAccion(String tipo, String mensaje, String accion){
            System.out.println(tipo+"         "+mensaje);
        }

        public void unrecovered_syntax_error(Symbol cur_token){
            if(cur_token.toString().equals("#0")){
                System.err.println("No se pudo recuperar y seguir analizando, no se encontraron más tokens, se llegó al final del archivo.");
            }else{
                System.err.println("No se pudo recuperar y seguir analizando, error con el token: "+cur_token.toString());
            }
        }

        public void syntax_error(Symbol s){
            String message = "";
            StringBuilder m = new StringBuilder("Error:");
            String cadena = (String) s.value;
            int linea = s.left;
            int columna = s.right;
            switch(s.toString()){
                //anotamos los diferentes mensajes dependiendo del simbolo que causó el problema
            }
            System.out.println("Se ha encontrado un error en la linea: "+linea+", columna: "+columna+" con la cadena "+cadena+"|Descripcion: "+message);
        }

        public List<String> getErrores(){
            return listErrores;
        }

        public void setErrores(List<String> e){
            listErrores = e;
        }
:}

/* terminales y no terminales */

terminal                                    PR_NOMBRE, PR_AUTOR, PR_VERSION, PR_LANZAMIENTO, PR_EXTENSION, ASIGNACION_INF, ASIGNACION_GRAMA;
terminal                                    ASIGNACION_ER, SEPARADOR, UNA_O_MAS_VECES, CERO_O_MAS_VECES, PUEDE_O_NO_PUEDE, O, COR_A, COR_C, CARACTER_EXPLICITO;
terminal                                    PAR_A, PAR_C, FIN_DE_LINEA, SALTO_DE_LINEA, TABULACION, RETORNO, IGNORAR, CADENA, JAVA_CODE, ENTERO;
terminal                                    VERSION, ID, RANGO_NUMEROS, RANGO_LETRAS_MIN, PR_TERMINAL, PR_NO, PR_ENTERO, PR_REAL, PR_CADENA, COMA;

non terminal                                s, seccionInformacion, dato, seccionER, expresionesRegulares, declaracionExpresionRegular;
non terminal                                expresionRegular, seccionSimbolos, declaracionesSimbolos, declaracionSimbolo, seccionGramatica;
non terminal                                produccion, listadoSimbolos, simboloProduccion, idCompuesto, tipoSimbolo, listadoIds, codigoJava;

/* precedencia */

precedence left     SALTO_DE_LINEA, TABULACION, RETORNO, CADENA;
precedence left     UNA_O_MAS_VECES, CERO_O_MAS_VECES, PUEDE_O_NO_PUEDE;
precedence left     O, RANGO_LETRAS_MIN, RANGO_NUMEROS;
precedence left     PAR_A, COR_A;
precedence left     CARACTER_EXPLICITO;

/* GRAMATICA */

start with s;

s::=                                        seccionInformacion SEPARADOR seccionER
                                            |error SEPARADOR seccionER
                                            ;

seccionInformacion::=                       seccionInformacion dato
                                            |dato
                                            ;

dato::=                                     PR_NOMBRE ASIGNACION_INF idCompuesto FIN_DE_LINEA
                                            |PR_LANZAMIENTO ASIGNACION_INF ENTERO FIN_DE_LINEA
                                            |PR_VERSION ASIGNACION_INF VERSION FIN_DE_LINEA
                                            |PR_AUTOR ASIGNACION_INF idCompuesto FIN_DE_LINEA
                                            |PR_EXTENSION ASIGNACION_INF ID FIN_DE_LINEA
                                            |error FIN_DE_LINEA
                                            ;

seccionER::=                                expresionesRegulares SEPARADOR seccionSimbolos
                                            |error SEPARADOR seccionSimbolos
                                            ;

expresionesRegulares::=                     expresionesRegulares declaracionExpresionRegular
                                            |declaracionExpresionRegular
                                            ;

declaracionExpresionRegular::=              ID ASIGNACION_ER expresionRegular FIN_DE_LINEA
                                            |IGNORAR ASIGNACION_ER expresionRegular FIN_DE_LINEA
                                            |error FIN_DE_LINEA
                                            ;

expresionRegular::=                         expresionRegular PUEDE_O_NO_PUEDE
                                            |expresionRegular CERO_O_MAS_VECES
                                            |expresionRegular UNA_O_MAS_VECES
                                            |expresionRegular O expresionRegular
                                            |expresionRegular expresionRegular
                                            |RANGO_LETRAS_MIN
                                            |RANGO_NUMEROS
                                            |SALTO_DE_LINEA 
                                            |TABULACION
                                            |RETORNO
                                            |CADENA
                                            |COR_A expresionRegular COR_C
                                            |PAR_A expresionRegular PAR_C
                                            |CARACTER_EXPLICITO
                                            ;


seccionSimbolos::=                          declaracionesSimbolos SEPARADOR seccionGramatica
                                            |error SEPARADOR seccionGramatica
                                            ;

declaracionesSimbolos::=                    declaracionesSimbolos declaracionSimbolo
                                            |declaracionSimbolo
                                            ;

declaracionSimbolo::=                       PR_TERMINAL tipoSimbolo listadoIds FIN_DE_LINEA
                                            |PR_NO PR_TERMINAL tipoSimbolo listadoIds FIN_DE_LINEA
                                            |error FIN_DE_LINEA
                                            ;

seccionGramatica::=                         seccionGramatica produccion
                                            |produccion
                                            ;

produccion::=                               ID ASIGNACION_GRAMA listadoSimbolos codigoJava FIN_DE_LINEA
                                            |error FIN_DE_LINEA
                                            ;

listadoSimbolos::=                          listadoSimbolos simboloProduccion
                                            |simboloProduccion
                                            ;

simboloProduccion::=                        ID ASIGNACION_INF ID
                                            |ID
                                            ;

idCompuesto::=                              idCompuesto ID
                                            |ID
                                            ;

tipoSimbolo::=                              PR_ENTERO
                                            |PR_REAL
                                            |PR_CADENA
                                            |
                                            ;

listadoIds::=                               listadoIds COMA ID
                                            |ID
                                            ;

codigoJava::=                               JAVA_CODE
                                            |
                                            ;